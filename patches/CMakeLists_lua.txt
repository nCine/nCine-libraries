cmake_minimum_required(VERSION 3.5)

project(lua C)

set(SRC_CORE src/lapi.c src/lcode.c src/lctype.c src/ldebug.c src/ldo.c src/ldump.c src/lfunc.c src/lgc.c src/llex.c
	src/lmem.c src/lobject.c src/lopcodes.c src/lparser.c src/lstate.c src/lstring.c src/ltable.c
	src/ltm.c src/lundump.c src/lvm.c src/lzio.c)
set(SRC_LIB src/lauxlib.c src/lbaselib.c src/lcorolib.c src/ldblib.c src/liolib.c src/lmathlib.c
	src/loadlib.c src/loslib.c src/lstrlib.c src/ltablib.c src/lutf8lib.c src/linit.c)

add_library(liblua_static ${SRC_CORE} ${SRC_LIB})
set_target_properties(liblua_static PROPERTIES OUTPUT_NAME lua)
target_compile_definitions(liblua_static PRIVATE LUA_COMPAT_5_3)

if(NOT EMSCRIPTEN)
	add_library(liblua SHARED ${SRC_CORE} ${SRC_LIB})
	target_compile_definitions(liblua PRIVATE LUA_COMPAT_5_3)
else()
	add_library(liblua ALIAS liblua_static)
	add_library(lua-mt ${SRC_CORE} ${SRC_LIB})
	target_compile_options(lua-mt PRIVATE "SHELL:-pthread")
	target_link_options(lua-mt PRIVATE "SHELL:-pthread")
endif()

if(WIN32 AND NOT CYGWIN)
	set_target_properties(liblua PROPERTIES OUTPUT_NAME lua54)
	target_compile_definitions(liblua PRIVATE LUA_BUILD_AS_DLL)
	if(MSVC)
		target_compile_definitions(liblua PRIVATE _CRT_SECURE_NO_WARNINGS)
	endif()
elseif(APPLE)
	set_target_properties(liblua PROPERTIES OUTPUT_NAME lua5.4)
	target_compile_definitions(liblua PRIVATE LUA_USE_MACOSX)
elseif(NOT EMSCRIPTEN)
	set_target_properties(liblua PROPERTIES OUTPUT_NAME lua5.4)
	target_compile_definitions(liblua PRIVATE LUA_USE_POSIX LUA_USE_DLOPEN)
	target_link_libraries(liblua m dl)
	target_link_libraries(liblua_static m dl)
endif()

include_directories(src)

set(SRC_LUA src/lua.c)
add_executable(lua ${SRC_LUA})
target_link_libraries(lua liblua)

set(SRC_LUAC src/luac.c)
add_executable(luac ${SRC_LUAC})
target_link_libraries(luac liblua_static)
